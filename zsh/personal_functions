#!/bin/zsh
# Functions

# Auto-rehashing.  Try command completion, if fails, rehash
function compctl_rehash { hash -r; reply=() }
compctl -C -c + -K compctl_rehash + -c

function ztmpl() {
  # $1 = template; $2 = output file
  if [[ $1 -nt $2 || (! -f $2 && -f $1) ]]; then
    echo "Using $1 as the template to make $2..."
    local foo="$(print -bP "$(cat $1)")"
    echo ${(e)foo} > $2
  fi
}

function wz() { 
  if $(which wget &>/dev/null); then
    wget "$1" -O- | tar xz
  elif $(which curl &>/dev/null); then
    curl "$1" | tar xz
  fi
}

function wj() { 
  if $(which wget &>/dev/null); then
    wget "$1" -O- | tar xj
  elif $(which curl &>/dev/null); then
    curl "$1" | tar xj
  fi
}

function wJ() { 
  if $(which wget &>/dev/null); then
    wget "$1" -O- | tar xJ
  elif $(which curl &>/dev/null); then
    curl "$1" | tar xJ
  fi
}

function confmake() {
  ./configure "$@" && make -j3
}

# If I am using vi keys, I want to know what mode I'm currently using.
# zle-keymap-select is executed every time KEYMAP changes.
# From http://zshwiki.org/home/examples/zlewidgets
function zle-line-init zle-keymap-select {
    VIMODE="${${KEYMAP/vicmd/c}/(main|viins)/i}"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# Executed before each prompt
function precmd() {
  vcs_info 'prompt'
  case $TERM in
    xterm*|*rxvt*|gnome*)
      print -Pn "\e]0;%n@%m: %~\a"
      ;;
  esac
  if [ -n "$VIRTUAL_ENV" ]; then
    virtual_env=[%{$fg[blue]%}$(basename "$VIRTUAL_ENV")%{$reset_color%}]
  else
    unset virtual_env
  fi
}

# Executed before each command
function preexec() {
  case $TERM in
    xterm*|*rxvt*|gnome*)
      print -n "\e]0;$1\a"
      ;;
  esac
}

if ps -u$USER -f | grep "sshd: $USER" | grep -v grep &>/dev/null; then
  eval ucolor='%{$fg[cyan]%}'
else
  eval ucolor='%{$fg[red]%}'
fi

function setprompt() {
  # Need this, so the prompt will work
  setopt prompt_subst

  # Finally, let's set the prompt
  PROMPT='┌─[$ucolor%m%{${reset_color}%}][%{$fg[green]%}%40<..<%~%{${reset_color}%}][%{$fg[yellow]%}${VIMODE}%{$reset_color%}]${vcs_info_msg_0_}${virtual_env}
└─> '
  #PROMPT='┌─[%{$fg[red]%}%m%{${reset_color}%}][%{$fg[green]%}%40<..<%~%{${reset_color}%}][%{$fg[yellow]%}${VIMODE}%{$reset_color%}]${vcs_info_msg_0_}${virtual_env}
#└─> '
}
