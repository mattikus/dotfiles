# Functions

function ztmpl() {
  # $1 = template; $2 = output file
  if [[ $1 -nt $2 || (! -f $2 && -f $1) ]]; then
    echo "Using $1 as the template to make $2..."
    local foo="$(print -bP "$(cat $1)")"
    echo ${(e)foo} > $2
  fi
}

function wz() { 
  if [[ -f $(which wget) ]]; then
    wget "$1" -O- | tar xzf -
  elif [[ -f $(which curl) ]]; then
    curl "$1" | tar xzf -
  fi
}

function wj() { 
  if [[ -f $(which wget) ]]; then
    wget "$1" -O- | tar xjf -
  elif [[ -f $(which curl) ]]; then
    curl "$1" | tar xjf -
  fi
}

function confmake() {
  ./configure "$@" && make -j3
}

# If I am using vi keys, I want to know what mode I'm currently using.
# zle-keymap-select is executed every time KEYMAP changes.
# From http://zshwiki.org/home/examples/zlewidgets
function zle-line-init zle-keymap-select {
    VIMODE="${${KEYMAP/vicmd/[c]}/(main|viins)/[i]}"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

if [[ ${ZSH_VERSION[-3,-1]} < 4.9 ]]; then
  function vcs_info() {}
fi

# Executed before each prompt
function precmd {
  vcs_info 'prompt'
  case $TERM in
    xterm*|*rxvt*|gnome*)
      print -Pn "\e]0;%1~\a"
      ;;
  esac
}

# Executed before each command
function preexec () {
  case $TERM in
    xterm*|*rxvt*|gnome*)
      print -n "\e]0;$1\a"
      ;;
  esac
}

function setprompt () {
  # Need this, so the prompt will work
  setopt prompt_subst

  # Finally, let's set the prompt
  PROMPT='%{$fg[magenta]%}%n %{$fg[white]%}at %{$fg[yellow]%}%m %{$fg[white]%}in %{$fg[green]%}%~ \

%{$fg[white]%}%%%{$reset_color%} '
  RPROMPT='${vcs_info_msg_0_}%{$fg[cyan]%}${VIMODE}%{$reset_color%}'
}


# vim:ft=zsh
